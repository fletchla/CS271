// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O. 
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory 
 * location specified by address. If load==1, the in value is loaded 
 * into the memory location specified by address. This value becomes 
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used. 
 * Access to address>0x6000 is invalid. Access to any address in 
 * the range 0x4000-0x5FFF results in accessing the screen memory 
 * map. Access to address 0x6000 results in accessing the keyboard 
 * memory map. The behavior in these addresses is described in the 
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    
	//last 2 elements of address indicate if ram1, ram2, keyboard, or screen
	//splitting address 13&14 4 ways, first two splits go to RAM
	DMux4Way(in=load, sel=address[13..14], a=goToRamA, b=goToRamB, c=sendLoadToScreen, d=loadkbd);
	
	//takes both RAM loads, both are used
	Or(a=goToRamA, b=goToRamB, out=sendLoadToRam);
	
	// RAM starts at 0000, ends at 0011
	//uses all but the last address
    RAM16K(in=in, load=sendLoadToRam, address=address[0..13], out=ramOut);
	
	//  Screen starts at 0100 ends at 0101
	//uses all but the last two addresses
    Screen(in=in, load=sendLoadToScreen, address=address[0..12], out=screenOut);
	
	//Keyboard is 011
	//keyboard magically gets load from DMux4Way chip, or that output goes nowhere
    Keyboard(out=keyboardOut);
	
	//takes screen and keyboard and RAM and provides output for the CPU
    Mux4Way16(a=ramOut, b=ramOut, c=screenOut, d=keyboardOut, sel=address[13..14], out=out);
}